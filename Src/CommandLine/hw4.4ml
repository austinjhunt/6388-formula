/* CS 6388 Homework 4 Due July 10, 2022
Austin J Hunt
*/

/*
Task 1) Family
Create a FORMULA domain for representing families:
-	Create new-kind constructors to represent Person.
-	Create a new-kind relationship Parent among two persons who are in child-parent relationship.
-	Add a conformance rule that enforces that a Person has at most 2 Parents.
*/


domain Family {
    // create constructor to represent a person
    Person ::= new (name: String).
    // create constructor to represent parent relationship among 2 persons who are in parent-child relationship
    // can be thought of as a directed edge from child to parent
    Parent ::= new (child: Person, parent: Person).

    // conformance rule enforcing that person has at most 2 parents
    // aka no person has more than 2 parents
    // for each child person, count number of parents, return true if > 2
    hasMoreThanTwoParents :- child is Person, count({parent | parent is Person(_), Parent(child, parent)}) > 2.
    conforms no hasMoreThanTwoParents.

}

model MyGoodFamily of Family {
    // this conforms
    ginger is Person("Ginger").
    don is Person("Donald").
    janice is Person("Janice").
    lisa is Person("Lisa").
    ron is Person("Ron").
    austin is Person("Austin").

    parentRel1 is Parent(austin, ron).
    parentRel2 is Parent(austin, lisa).
    parentRel3 is Parent(lisa, janice).
    parentRel4 is Parent(ron, don).
    parentRel5 is Parent(ron, ginger).
}

model MyBadFamily of Family {
    // this does not conform. ron has 3 parents.
    ginger is Person("Ginger").
    don is Person("Donald").
    arnold is Person("Arnold").
    janice is Person("Janice").
    lisa is Person("Lisa").
    ron is Person("Ron").
    austin is Person("Austin").

    parentRel1 is Parent(austin, ron).
    parentRel2 is Parent(austin, lisa).
    parentRel3 is Parent(lisa, janice).
    parentRel4 is Parent(ron, don).
    parentRel5 is Parent(ron, ginger).
    parentRel6 is Parent(ron, arnold).
}

/* execute the following to test model conformity
query MyGoodFamily Family.conforms
query MyBadFamily Family.conforms

produced expected results
25 | Query |  Done  |  true  | 7/1/2022 6:43 PM |  0.01s   
26 | Query |  Done  | false  | 7/1/2022 6:43 PM |  0.02s   
*/



/*
Task 2) AlgTree
Using the AlgTrees domain from the FORMULA manual (Example 2.4 on page 21),
add a constraint that checks if a tree is not higher/deeper than 2 levels.

*/

domain AlgTrees {
    Node ::= new (left: any Node + {NIL}, right: any Node + {NIL}).
    Root ::= new (root: any Node).

    // relationship indicating first node has the second node as either left or right child
    parent ::= (n1: Node, n2: Node + {NIL}).
    parent(n1, n2) :- n1 is Node(_, n2);  n1 is Node(n2, _).

    // define the height of the tree
    height ::= (n: Node + {NIL}, h: Integer).
    height(n, h) :- n is Node(_, _), r is Root(n), h = 0.
    height(n, h) :- n is Node(_, _), height(m, x), parent(m, n), h = x + 1.

    // if height is between 3 ..... m
    // then height(n, [1,2,3,....m]) will be true.
    heightMoreThanTwo :- height(n, d), d > 2.

    conforms no heightMoreThanTwo.
    
    // if any of the nodes on the second level have children, too deep (> 2 levels)
    // this is a test for the single value tree models, i.e. Root(Node(...)) that does not 
    // use alias notation. above does not work for non-alias notation models.
    tooDeep :- rn is Node(_, _), r is Root(rn), parent(rn.left.left, _);
            rn is Node(_, _), r is Root(rn), parent(rn.left.right, _);
            rn is Node(_, _), r is Root(rn), parent(rn.right.left, _);
            rn is Node(_, _), r is Root(rn), parent(rn.right.right, _).
    conforms no tooDeep. 
    // still does not work :/ 
    // the "Bad Trees" are showing false still for both "tooDeep" and for "heightMoreThanTwo"
}

model BadTree of AlgTrees {
    n4 is Node(NIL, NIL).
    n3 is Node(n4, NIL).
    n2 is Node(NIL, n3).
    n1 is Node(n2, NIL).
    n0 is Node(n1, NIL).
    r is Root(n0).
}

model GoodTree of AlgTrees {
    n2 is Node(NIL, NIL).
    n1 is Node(n2, NIL).
    n0 is Node(n1, NIL).
    r is Root(n0).
}

/*
execute the following to test model conformity.
query BadTree AlgTrees.conforms
query GoodTree AlgTrees.conforms

produced expected results
 23 | Query |  Done  | false  | 7/1/2022 6:43 PM |  0.01s   
 24 | Query |  Done  |  true  | 7/1/2022 6:43 PM |  0.00s   
*/

/* while the above two are properly
classified as non-conformant models,
the below are not properly classified.
i believe it's because defining the entire tree
as one single value starting at Root(...)
breaks the constraint defined in the domain.
I am not sure how to fix that.
*/

model GoodTree1 of AlgTrees {
    Root(
        Node(
            Node(NIL, NIL),
            Node(NIL, NIL)
        )
    ).
}
model GoodTree2 of AlgTrees {
    // 2 levels
    Root(
        Node(
            Node(
                Node(NIL, NIL), NIL),
            Node(NIL, NIL)
        )
    ).
}
model BadTree1 of AlgTrees {
    // 3 levels
    Root(
        Node(
            Node(
                Node(
                    Node(NIL, NIL),
                    NIL),
                Node(NIL, NIL)
            ),
            Node(NIL, NIL)
        )
    ).
}
model BadTree2 of AlgTrees {
    Root(
        Node(
            Node(
                Node(
                    Node(NIL, NIL),
                    NIL),
                Node(NIL, NIL)
            ),
            Node(
                Node(
                    Node(
                        Node(NIL, NIL),
                         NIL),
                    NIL),
                Node(NIL, NIL)
            )
        )
    ).
}
/*
execute the following to test model conformity.
query GoodTree1 AlgTrees.conforms
query GoodTree2 AlgTrees.conforms
query BadTree1 AlgTrees.conforms
query BadTree2 AlgTrees.conforms

These did not produce the expected results, and I 
believe it is because these models are not using the alias notation
but rather defining the entire tree as a single value starting with Root(...)

technically these first two are correct (they do conform)
but i don't think it's right for the right reason 
29 | Query |  Done  |  true  | 7/1/2022 6:47 PM |  0.00s   
30 | Query |  Done  |  true  | 7/1/2022 6:47 PM |  0.00s   
31 | Query |  Done  |  true  | 7/1/2022 6:47 PM |  0.00s   
32 | Query |  Done  |  true  | 7/1/2022 6:47 PM |  0.00s 
*/

/* Task 3) FSM
Create a FORMULA domain to represent finite state machines:
-	States should use an integer identifier.
-	Directed Transitions should be a relation between states.
-	Final and Initial states should take a State parameter.
-	Conformance rules must check for:
o	There is exactly one initial state.
o	From the initial state it is possible to reach at least one final state.
o	Any state is reachable from the initial state
    (if we assume that a node is reachable from itself,
    otherwise every node except the initial state must be reachable)
*/

domain FSM {
    State ::= new (id: Integer).
    Transition ::= new (src: State, dest: State).
    Final ::= new (finalState: State).
    Initial ::= new (initState: State).

    // There is exactly one initial state.
    exactlyOneInitial :- count({init | init is Initial}) = 1.
    conforms exactlyOneInitial.

    
    path ::= (src: State, dest: State).
    path(src, dest) :- Transition(src, dest) ; intermediate is State, Transition(src, intermediate), path(intermediate, dest). 

    // from the initial state it is possible to reach at least one final state
    finalReachable :- s1 is State, s2 is State, init is Initial(s1), count({f | f is Final(s2), path(s1, s2)}) >= 1.
    conforms finalReachable.

    // assumption: init node not reachable from itself
    initSelfLoopExists :- s is State, i is Initial(s), path(s, s). 
    conforms no initSelfLoopExists.

    reachable ::= (State, Integer). 
    reachable(s1, c) :- i is Initial(s1), c = count({s | path(s1, s)}). 
    /*
    Any state is reachable from the initial state
    (if we assume that a node is reachable from itself,
    otherwise every node except the initial state must be reachable)
    */
    /* we made the assumption that init has no self-transition
     this means the count of states reachable from init must be equal
    to the total count of the states minus one (the initial state)
    */
    totalStateCount ::= (c: Integer).
    totalStateCount(c) :- c = count({s | s is State}).

    // allStatesReachable :-  s1 is State, i is Initial(s1), count({s | s is State(_), path(i, s)}) = count({s | s is State}) - 1.
    allStatesReachable :- s1 is State, i is Initial(s1), totalStateCount(totalCount), reachable(s1, reachableCount), reachableCount = totalCount - 1. 
    conforms allStatesReachable.
}

model GoodFSM1 of FSM {

    s0 is State(0),
    s1 is State(1),
    s2 is State(2),
    s3 is State(3),
    s4 is State(4),
    s5 is State(5).
    s6 is State(6).

    init is Initial(s0).
    final is Final(s6).

    t1 is Transition(s0, s1),
    t2 is Transition(s1, s2),
    t3 is Transition(s2, s3),
    t4 is Transition(s3, s4),
    t5 is Transition(s4, s5),
    t6 is Transition(s5, s6).

}
model GoodFSM2 of FSM {
    s0 is State(0),
    s1 is State(1),
    s2 is State(2),
    s3 is State(3),
    s4 is State(4),
    s5 is State(5).
    s6 is State(6).

    init is Initial(s0).
    final is Final(s6).

    t1 is Transition(s0, s1),
    t2 is Transition(s1, s2),
    t3 is Transition(s2, s3),
    t4 is Transition(s3, s4),
    t5 is Transition(s4, s5),
    t6 is Transition(s5, s6).

}
model GoodFSM3 of FSM {
    s0 is State(0),
    s1 is State(1),
    s2 is State(2),
    s3 is State(3),
    s4 is State(4),
    s5 is State(5).
    s6 is State(6).

    init is Initial(s0).
    final is Final(s6).

    t1 is Transition(s0, s1),
    t2 is Transition(s1, s2),
    t3 is Transition(s2, s3),
    t4 is Transition(s3, s4),
    t5 is Transition(s4, s5),
    t6 is Transition(s5, s6).
}
model BadFSM1 of FSM {
    s0 is State(0),
    s1 is State(1),
    s2 is State(2),
    s3 is State(3),
    s4 is State(4),
    s5 is State(5).
    s6 is State(6).

    init is Initial(s0).
    final is Final(s6).

    t1 is Transition(s0, s1),
    t2 is Transition(s1, s2),
    t3 is Transition(s2, s3),
    t4 is Transition(s3, s4),
    t5 is Transition(s4, s5),
    t6 is Transition(s5, s6).


    // two initial states is bad.
    init2 is Initial(s1).
}
model BadFSM2 of FSM {
    s0 is State(0),
    s1 is State(1),
    s2 is State(2),
    s3 is State(3),
    s4 is State(4),
    s5 is State(5).
    s6 is State(6).

    init is Initial(s0).
    final is Final(s6).

    t1 is Transition(s0, s1),
    t2 is Transition(s1, s2),
    t3 is Transition(s2, s3),
    t4 is Transition(s3, s4),
    t5 is Transition(s4, s5),
    t6 is Transition(s5, s6).

    // init node reachable from self
    t7 is Transition(s0, s0).
}
model BadFSM3 of FSM {
    s0 is State(0),
    s1 is State(1),
    s2 is State(2),
    s3 is State(3),
    s4 is State(4),
    s5 is State(5).
    s6 is State(6).

    init is Initial(s0).
    final is Final(s6).

    // not all nodes reachable from init. s3 skipped.
    t1 is Transition(s0, s1),
    t2 is Transition(s1, s2),
    t3 is Transition(s2, s4),
    t4 is Transition(s4, s5),
    t5 is Transition(s5, s6).
}

model BadFSM4 of FSM {
    s0 is State(0),
    s1 is State(1),
    s2 is State(2),
    s3 is State(3),
    s4 is State(4),
    s5 is State(5).
    s6 is State(6).
    s7 is State(7).

    init is Initial(s0).
    final is Final(s6).
    final2 is Final(s7).

    t1 is Transition(s0, s1),
    t2 is Transition(s1, s2),
    t3 is Transition(s2, s3),
    t4 is Transition(s3, s4),
    t5 is Transition(s4, s5).

    // no final state reachable from init.

    // t6 is Transition(s5, s6).
    // t7 is Transition(s5, s7).
}
/*
execute the following to test model conformity

query GoodFSM1 FSM.conforms
query GoodFSM2 FSM.conforms
query GoodFSM3 FSM.conforms
query BadFSM1 FSM.conforms
query BadFSM2 FSM.conforms
query BadFSM3 FSM.conforms
query BadFSM4 FSM.conforms

All of these produced expected results
 16 | Query |  Done  |  true  | 7/1/2022 6:41 PM |  0.03s   
 17 | Query |  Done  |  true  | 7/1/2022 6:41 PM |  0.04s   
 18 | Query |  Done  |  true  | 7/1/2022 6:41 PM |  0.05s   
 19 | Query |  Done  | false  | 7/1/2022 6:41 PM |  0.03s   
 20 | Query |  Done  | false  | 7/1/2022 6:42 PM |  0.03s   
 21 | Query |  Done  | false  | 7/1/2022 6:42 PM |  0.02s   
 22 | Query |  Done  | false  | 7/1/2022 6:42 PM |  0.02s  
*/